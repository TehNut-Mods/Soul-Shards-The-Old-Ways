buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:0.10.1'
    }
}

apply plugin: 'forge'
apply plugin: 'curseforge'
apply plugin: 'maven-publish'

def build_number = 'CUSTOM'

if (System.getenv('BUILD_NUMBER') != null)
    build_number = System.getenv('BUILD_NUMBER')

group = package_group
archivesBaseName = mod_name
version = "${mc_version}-${mod_version}-${build_number}"

import org.ajoberstar.grgit.Grgit

def gitHash = 'unknown'
if (new File(projectDir, '.git').exists()) {
    def repo = Grgit.open(project.file('.'))
    gitHash = repo.log().find().abbreviatedId
}

repositories {
	maven { url "http://chickenbones.net/maven/" }
	maven { url "http://mobiusstrip.eu/maven" }
    maven { url 'http://dvs1.progwml6.com/files/maven' }

    ivy {
        name "Baubles"
        artifactPattern "https://dl.dropboxusercontent.com/u/47135879/[module]-deobf-1.7.10-[revision].[ext]"
    }
}

dependencies {
    compile name: 'Baubles', version: "${baubles_version}", ext: 'jar'
	compile "mcp.mobius.waila:Waila:${waila_version}_${mc_version}:dev"
	compile "codechicken:CodeChickenCore:${mc_version}-${ccc_version}:dev"
	compile "codechicken:NotEnoughItems:${mc_version}-${nei_version}:dev"
	compile "tconstruct:TConstruct:${mc_version}-${tcon_version}:deobf"
	compile fileTree(dir: 'libs', include: ['*jar'])
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

	replaceIn "Reference.java"
    replace "@VERSION@", project.version
	
    if (project.hasProperty('mappings_version'))
        mappings = project.mappings_version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

jar {
    classifier = ''
    manifest {
        attributes 'FMLCorePlugin': 'com.whammich.sstow.asm.LoadingPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
    manifest.mainAttributes(
            "Built-By": System.getProperty('user.name'),
            "Created-By": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Git-Hash": gitHash
    )
}

// Source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// Javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Deobf jar
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn sourceJar, javadocJar, deobfJar

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourceJar
            artifact javadocJar
            artifact deobfJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}

curse {
    projectId = '226958'
	if (project.hasProperty('curse_key'))
		apiKey = project.curse_key
    releaseType = 'release'

    changelog = project.hasProperty('changelog') ? project.changelog : ''
    relatedProject 'notenoughitems': 'optionalLibrary'
    relatedProject 'waila': 'optionalLibrary'
}